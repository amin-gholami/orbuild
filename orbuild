#!/usr/bin/perl

=head1 OVERVIEW

orbuild version 1.08

Build system for OpenRISC - see the README file for more information.

=head1 USAGE

  ./orbuild [options]

=head1 OPTIONS

-h, --help, --version, --license, --test-visual-notification,

--resume, --restart, --interactive

Please see the README file for more information.

=head1 EXIT CODE

Exit code: 0 on success, some other value on error.

=head1 FEEDBACK

Please send feedback to rdiezmail-openrisc at yahoo.de

=head1 LICENSE

Copyright (C) 2011 R. Diez

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU Affero General Public License version 3 as published by
the Free Software Foundation.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU Affero General Public License version 3 for more details.

You should have received a copy of the GNU Affero General Public License version 3
along with this program.  If not, see L<http://www.gnu.org/licenses/>.

=cut

use strict;
use warnings; 
use integer;  # There is no reason to resort to floating point in this script.

use Getopt::Long;
use IO::Handle;
use FindBin;
use Cwd;

use constant SANDBOX_DIR => File::Spec->rel2abs( $FindBin::Bin );

use lib "@{[SANDBOX_DIR]}/Scripts/PerlModules";
use MiscUtils;
use StringUtils;
use ProcessUtils;
use ConfigFile;
use AGPL3;

use constant SCRIPT_NAME => $0;

use constant APP_NAME    => "orbuild";
use constant APP_VERSION => "1.08";  # If you update it, update also the perldoc text above.

# In case the shebang fails in weird operating systems, use the explicit interpreter names.
use constant SHELL_INTERPRETER => "bash";
use constant PERL_INTERPRETER  => "perl";

use constant SCRIPTS_DIR => FileUtils::cat_path( SANDBOX_DIR, "Scripts" );
use constant TOOLS_DIR   => FileUtils::cat_path( SCRIPTS_DIR, "Tools" );

use constant BUILD_INTERNAL_REPORTS_SUBDIR    => "OrbuildInternalReports";
use constant BUILD_PUBLIC_REPORTS_BASEDIR     => "OrbuildPublicReports";
use constant BUILD_PUBLIC_REPORTS_SUBDIR      => "Reports";
use constant BUILD_SENTINELS_SUBDIR           => "OrbuildSentinels";
use constant SENTINELS_FILENAME_SUFFIX        => "OrbuildSentinel";

use constant BUILD_CHECKOUT_SENTINELS_SUBDIR  => FileUtils::cat_path( BUILD_SENTINELS_SUBDIR, "Checkout" );
use constant BUILD_UNPACK_SENTINELS_SUBDIR    => FileUtils::cat_path( BUILD_SENTINELS_SUBDIR, "Unpack"   );
use constant BUILD_AUTOCONF_SENTINELS_SUBDIR  => FileUtils::cat_path( BUILD_SENTINELS_SUBDIR, "Autoconf" );
use constant BUILD_COMMAND_SENTINELS_SUBDIR   => FileUtils::cat_path( BUILD_SENTINELS_SUBDIR, "Commands" );

use constant SANDBOX_LAYOUT_VERSION_FILENAME => FileUtils::cat_path( SANDBOX_DIR, "orbuild.version" );
use constant SANDBOX_LAYOUT_VERSION_NUMBER   => 2;

use constant STATUS_FILENAME => FileUtils::cat_path( SANDBOX_DIR, "orbuild.status" );
use constant CONFIG_FILENAME => FileUtils::cat_path( SANDBOX_DIR, "orbuild.conf" );

use constant DATE_SEPARATOR => "-";
use constant TIME_SEPARATOR => ":";


# ----------- main routine, the script entry point is at the bottom -----------

sub main ()
{
  my $arg_help             = 0;
  my $arg_h                = 0;
  my $arg_version          = 0;
  my $arg_license          = 0;
  my $arg_interactive      = 0;
  my $arg_resume           = 0;
  my $arg_restart          = 0;
  my $arg_testVisualNotification = 0;


  my $result = GetOptions(
                 'help'                     => \$arg_help,
                 'h'                        => \$arg_h,
                 'version'                  => \$arg_version,
                 'license'                  => \$arg_license,
                 'interactive'              => \$arg_interactive,
                 'test-visual-notification' => \$arg_testVisualNotification,
                 'resume'                   => \$arg_resume,
                 'restart'                  => \$arg_restart
                );

  if ( not $result )
  {
    # GetOptions has already printed an error message.
    return MiscUtils::EXIT_CODE_FAILURE_ARGS;
  }

  if ( $arg_help || $arg_h )
  {
    write_stdout( "\n" . MiscUtils::get_cmdline_help_from_pod( SCRIPT_NAME ) );
    return MiscUtils::EXIT_CODE_SUCCESS;
  }

  if ( $arg_version )
  {
    write_stdout( "@{[APP_NAME]} version @{[APP_VERSION]}\n" );
    return MiscUtils::EXIT_CODE_SUCCESS;
  }

  if ( $arg_license )
  {
    write_stdout( AGPL3::get_agpl3_license_text() );
    return MiscUtils::EXIT_CODE_SUCCESS;
  }

  if ( $arg_resume and $arg_restart )
  {
    die "Switches --resume and --restart cannot be specified at the same time.\n";
  }

  if ( 0 != scalar @ARGV )
  {
    die "Invalid number of arguments. Run this program with the --help option for usage information.\n";
  }

  if ( SANDBOX_DIR =~ m/\s/ )
  {
    die "The orbuild sandbox path \"@{[SANDBOX_DIR]}\" contains whitespace, but GNU make cannot cope with whitespaces.\n";
  }


  my %configEntries;
  process_config_file_etc( \%configEntries, $arg_interactive );

  my $exitCode;

  if ( $arg_testVisualNotification )
  {
    my $reportPath = FileUtils::cat_path( TOOLS_DIR, "TestReportForVisualNotificationTest.html" );
    run_visual_notification( $reportPath, \%configEntries, MiscUtils::TRUE );
    $exitCode = MiscUtils::EXIT_CODE_SUCCESS;
  }
  else
  {
    $exitCode = build_command( $arg_resume, $arg_restart, \%configEntries );
  }

  return $exitCode;
}


sub read_uptime ()
{
  my $filename = "/proc/uptime";

  my @lines = FileUtils::read_text_file( $filename );

  if ( 0 == scalar( @lines ) )
  {
    die "File $filename is empty.\n";
  }

  my $uptimeStr = $lines[0];

  my @seconds = $uptimeStr =~ m/(\d+)/o;

  if ( 1 != scalar( @seconds ) )
  {
    die "Cannot parse contents of $filename: \"$uptimeStr\".\n";
  }

  return $seconds[0];
}


sub read_failed_count ( $ )
{
  my $filename = shift;

  my @lines = FileUtils::read_text_file( $filename );

  if ( 0 == scalar( @lines ) )
  {
    die "File $filename is empty.\n";
  }

  if ( 1 != scalar( @lines ) )
  {
    die "File $filename has unexpected lines.\n";
  }

  my $failedCountStr = $lines[0];

  my @components = $failedCountStr =~ m/^(\d+)$/o;

  if ( 1 != scalar( @components ) )
  {
    die "Cannot parse contents of $filename: \"$failedCountStr\".\n";
  }

  return $components[0];
}


sub build_command ( $ $ $ )
{
  my $resume        = shift;
  my $restart       = shift;
  my $configEntries = shift;

  my $startUptime = read_uptime();
  
  # If there is a status file, and the user did not specify whether he wants --resume or --restart,
  # it's not clear what we should do.

  if ( !$resume and !$restart and -f STATUS_FILENAME )
  {
    my %temp;
    read_status( \%temp );

    my $lastAttempt = $temp{ "LastAttempt" };

    if ( $lastAttempt eq "successful" )
    {
      write_stdout( "The last build attempt finished successfully, the results are here:\n" .
                    "  " . $temp{ "CurrentBuildDir" } . "\n" .
                    "If you wish to start a new build from scratch, please specify the --restart switch.\n" );
      return MiscUtils::EXIT_CODE_FAILURE_ERROR;
    }
    else
    {
      write_stdout( "The last build attempt failed and no operation was explicitly specified.\n" .
                    "If you wish to resume from the last point of failure, please specify the --resume switch " .
                    "(but see the documentation for caveats).\n" .
                    "Otherwise, please specify the --restart switch in order to start a new build from scratch.\n" );

      return MiscUtils::EXIT_CODE_FAILURE_ERROR;
    }
  }

  if ( $resume and ! -f STATUS_FILENAME )
  {
    write_stdout( "You specified the --resume switch, but there is no previous failed build to resume.\n" );
    return MiscUtils::EXIT_CODE_FAILURE_ERROR;
  }


  my $rotate = $resume ? MiscUtils::FALSE : MiscUtils::TRUE;

  if ( $rotate )
  {
    write_stdout( "Rotating the build directory...\n" );

    # The 'new' command was permanently replaced by 'rotate'. The old 'new' command
    # used to pass MiscUtils::FALSE here:
    rotate_build_dir( $configEntries, MiscUtils::TRUE );
  }

  read_status( $configEntries );

  my $buildDir = $configEntries->{ "CurrentBuildDir" };

  if ( not -d $buildDir )
  {
    die "The build directory \"$buildDir\" does not exist. " .
        "Rerun @{[SCRIPT_NAME]} with the '--restart' switch in order to create a new one.\n";
  }

  write_stdout( "The current build directory is: $buildDir\n" );

  my $latestBuildDirLinkPath = File::Spec->rel2abs( FileUtils::cat_path( $configEntries->{ "BuildsDir" }, "LatestBuildDir" ) );
  create_symbolic_link( $latestBuildDirLinkPath, $buildDir );


  my $downloadCacheDir   = File::Spec->rel2abs( $configEntries->{ "DownloadCacheDir" }, SANDBOX_DIR );
  my $repositoriesDir    = File::Spec->rel2abs( $configEntries->{ "RepositoriesDir"  }, SANDBOX_DIR );

  my $publicReportsBasedir = FileUtils::cat_path( $buildDir, BUILD_PUBLIC_REPORTS_BASEDIR );
  my $publicReportsDir     = FileUtils::cat_path( $publicReportsBasedir, BUILD_PUBLIC_REPORTS_SUBDIR );
  my $internalReportsDir   = FileUtils::cat_path( $buildDir, BUILD_INTERNAL_REPORTS_SUBDIR );

  my $makefileLogFile    = FileUtils::cat_path( $publicReportsDir  , "TopLevelMakefileLogFile.txt" );
  my $makefileReportFile = FileUtils::cat_path( $internalReportsDir, "TopLevelMakefile.report"     );

  FileUtils::create_folder_if_does_not_exist( $downloadCacheDir );
  FileUtils::create_folder_if_does_not_exist( $repositoriesDir  );
  # The git clone sentinel files no longer live in a separate directory:
  #   FileUtils::create_folder_if_does_not_exist( FileUtils::cat_path( $repositoriesDir, "GitCloneSentinels" )  );

  my $makefileFilename = FileUtils::cat_path( SANDBOX_DIR, $configEntries->{ "Makefile" } );

  # If the makefile is not found, GNU Make's error message is long and confusing,
  # so check manually here in order to generate a clean error message.
  if ( not -f $makefileFilename )
  {
    die "The makefile specified in the configuration ($makefileFilename) does not exist.\n";
  }

  my ( $makefileVolume, $makefileDirectories, $makefileFilenameOnly ) = File::Spec->splitpath( $makefileFilename );
  my $makefileDir = FileUtils::cat_path( $makefileVolume, $makefileDirectories );

  $makefileDir = StringUtils::str_remove_optional_suffix( $makefileDir, "/" );

  # We already have an implementation in bash of how to get the number of CPUs,
  # so use it here, instead of reimplementing it in perl.
  my $cmdJParam = SHELL_INTERPRETER . " -c \"set -o errexit && source \"@{[SCRIPTS_DIR]}/ShellModules/MakeJVal.sh\" && get_make_j_val MAKE_J_VAL && echo \\\$MAKE_J_VAL\"";
  my $makeJParam = ProcessUtils::run_process_capture_single_value_output( $cmdJParam );

  my $makeFlags = "--no-builtin-variables ";  # Accelerates the dependency processing.
  $makeFlags   .= "--warn-undefined-variables ";

  # Disable the silent built. I think the user should see the actual commands issued in the log file.
  #   $makeFlags   .= "-s ";  # Silent.

  my $stopOnFirstError = ( lc( $configEntries->{ "StopOnFirstError" } ) eq "no" ) ? 0 : 1;

  if ( not $stopOnFirstError )
  {
    $makeFlags   .= "-k ";
  }

  if ( lc($configEntries->{ "ParallelBuild" }) eq "yes" )
  {
    # If the user did not specify the parallel build option, do NOT add any -j parameter.
    # This way, a top-level makefile invoking orbuild can control the parallel build
    # with the MAKEFLAGS environment variable, just like orbuild tries to do with its own
    # sub-make child processes.
    #
    # Option --max-load is useful in order to avoid increasing the load on an already-overloaded PC.
    # Keep in mind that some of the orbuild sub-tasks invoke GNU Make with their own -j parameter values,
    # which means that orbuild itself will probably exceed its own -j limit on the system.
    #
    # Note that argument --max-load alone is not enough, as most systems have a delay
    # when calculating the current load, which may cause too many child processes to get started.
    #
    # Note also that --max-load means orbuild will not get a fair share of CPU time, as any
    # other system load will take priority. That's usually fine, as orbuild is normally executed as
    # a long-running background task that should not interfere too much with the system's main tasks.
    #
    # If you are serious about constraining your system load, you'll have to resort to
    # more powerful methods like cgroups under Linux.

    $makeFlags   .= "-j $makeJParam ";

    # Cygwin's port of GNU Make does not support the --max-load switch (as of April 2012).
    # It does accept it, but then it prints a warning message.
    if ( !is_cygwin() )
    {
      my $maxLoadJParam = $makeJParam + 5;  # This heuristic is not very smart.
      $makeFlags   .= "--max-load $maxLoadJParam ";
    }
  }
  # $makeFlags .= "--debug=v ";  # For make debugging purposes

  my $noStdInput = ( lc($configEntries->{ "InteractiveBuild" }) eq "yes" ) ? "" : "</dev/null";

  my $componentGroupsFilename = FileUtils::cat_path( $internalReportsDir, "ComponentGroups.lst" );
  my $subprojectsFilename     = FileUtils::cat_path( $internalReportsDir, "Subprojects.lst" );

  # Create the files with 0 length, or truncate them if they already exist.
  FileUtils::write_string_to_new_file( $componentGroupsFilename, "" );
  FileUtils::write_string_to_new_file( $subprojectsFilename, "" );

  # Export some variables for the project Makefile and for other orbuild tools.
  #
  # These variables are generic to the build process:
  $ENV{ ORBUILD_SANDBOX                    } = SANDBOX_DIR;
  $ENV{ ORBUILD_BUILD_DIR                  } = $buildDir;
  $ENV{ ORBUILD_DOWNLOAD_CACHE_DIR         } = $downloadCacheDir;
  $ENV{ ORBUILD_REPOSITORIES_DIR           } = $repositoriesDir;
  $ENV{ ORBUILD_REPOSITORIES_TIMESTAMP     } = ""; # Timestamp support has not been fully developed yet, see the README file for more information.
  $ENV{ ORBUILD_TOOLS                      } = TOOLS_DIR;
  $ENV{ ORBUILD_SENTINEL_FILENAME_SUFFIX   } = SENTINELS_FILENAME_SUFFIX;
  $ENV{ ORBUILD_PROJECT_DIR                } = $makefileDir;
  $ENV{ ORBUILD_STOP_ON_FIRST_ERROR        } = $stopOnFirstError;


  # These variables are used by some of the tools bundled with orbuild:
  $ENV{ ORBUILD_INTERNAL_REPORTS_DIR   } = $internalReportsDir;
  $ENV{ ORBUILD_PUBLIC_REPORTS_DIR     } = $publicReportsDir;

  $ENV{ ORBUILD_CHECKOUT_SENTINELS_DIR } = FileUtils::cat_path( $buildDir, BUILD_CHECKOUT_SENTINELS_SUBDIR );
  $ENV{ ORBUILD_UNPACK_SENTINELS_DIR   } = FileUtils::cat_path( $buildDir, BUILD_UNPACK_SENTINELS_SUBDIR );
  $ENV{ ORBUILD_BUILD_SENTINELS_DIR    } = FileUtils::cat_path( $buildDir, BUILD_AUTOCONF_SENTINELS_SUBDIR );
  $ENV{ ORBUILD_COMMAND_SENTINELS_DIR  } = FileUtils::cat_path( $buildDir, BUILD_COMMAND_SENTINELS_SUBDIR );

  $ENV{ ORBUILD_IS_INTERACTIVE_BUILD      } = ( lc($configEntries->{ "InteractiveBuild" }) eq "yes" ) ? "1" : "0";
  $ENV{ ORBUILD_COMPONENT_GROUPS_FILENAME } = $componentGroupsFilename;
  $ENV{ ORBUILD_SUBPROJECTS_FILENAME      } = $subprojectsFilename;

  # Most build failures under Cygwin are due to spaces in the PATH, as most Windows PC have something
  # like "C:\Program Files" in there. In order to test that all components can cope with this situation,
  # add here a non-existent directory name with a space in the middle.
  if ( $ENV{ PATH } eq "" )
  {
    die "The PATH environment variable is empty.\n";
  }
  $ENV{ PATH } = $ENV{ PATH } . ":" . FileUtils::cat_path( $buildDir, "TestPathEntry WithASpaceInTheMiddle" );

  my $cmd = "@{[SHELL_INTERPRETER]} @{[TOOLS_DIR]}/RunAndReport.sh " .
                        "TOP_LEVEL_MAKEFILE " .
                        "\"top-level makefile (all log files combined)\" " .
                        "$makefileLogFile " . 
                        "$makefileReportFile " .
                        "report-always " .
                "make " .
                    "-C \"$buildDir\" " .
                    "$makeFlags " .
                    "-f \"$makefileFilename\" " .
                    "all " .
                    "$noStdInput";

  # Lower our CPU priority. Builds are normally long-running processes that should not interfere
  # too much with other system load.
  use constant PRIO_PROCESS => 0;
  my $currentPrio = getpriority( PRIO_PROCESS, $$ );
  # 15 = 5 * 3, that means under Linux that the default ionice priority is automatically reduced by 3 notches,
  # which should also help reduce the hard disk performace impact on other applications.
  setpriority( PRIO_PROCESS, $$, $currentPrio + 15 );

  my $makeExitCode = ProcessUtils::run_process( $cmd );

  my $reportFilename = "BuildReport.html";

  my $elapsedMsg = MiscUtils::human_friendly_elapsed_time( read_uptime() - $startUptime );
  my $failedCountFilename = FileUtils::cat_path( $publicReportsBasedir, "FailedCount.report" );

  my $reportCmd = "@{[PERL_INTERPRETER]} @{[TOOLS_DIR]}/GenerateBuildReport.pl " .
                  "--topLevelReportFilename $makefileReportFile " .
                  "--componentGroupsFilename $componentGroupsFilename " .
                  "--subprojectsFilename $subprojectsFilename " .
                  "--elapsedTime \"$elapsedMsg\" " .
                  "--failedCountFilename \"$failedCountFilename\" " .
                  "$internalReportsDir " .
                  "$publicReportsBasedir " .
                  "@{[BUILD_PUBLIC_REPORTS_SUBDIR]} " .
                  "$reportFilename";

  ProcessUtils::run_process_exit_code_0( $reportCmd );

  my $failedCount = read_failed_count( $failedCountFilename );

  # Do not create the LatestBuildReport.html symbolic link any more, as it does not work if the user
  # tries to click through to the individual build logs.
  #   my $latestReportLinkPath = File::Spec->rel2abs( FileUtils::cat_path( $configEntries->{ "BuildsDir" }, "LatestBuildReport.html" ) );
  #   create_symbolic_link( $latestReportLinkPath, $reportFilePath );


  # write_stdout( "---------------------------------------\n" );

  my $reportFilePathOverLatestBuildDirLink = FileUtils::cat_path( $latestBuildDirLinkPath, BUILD_PUBLIC_REPORTS_BASEDIR, $reportFilename );
  my $reportFileMsg;

  if ( is_cygwin() )
  {
    my $winPath = ProcessUtils::run_process_capture_single_value_output( "cygpath -w \"$reportFilePathOverLatestBuildDirLink\"" );
    $reportFileMsg = "Cygwin : $reportFilePathOverLatestBuildDirLink\n" .
                     "Windows: $winPath\n";
  }
  else
  {
    $reportFileMsg = "  $reportFilePathOverLatestBuildDirLink\n";
  }

  my $error;
  my $finalMsg;

  if ( $failedCount != 0 )
  {
    # When a component fails, it makes the top-level makefile fail too, which is not ideal. This is because
    # each component is actually a GNU Make target. A failed target must return a non-zero exit code,
    # otherwise GNU Make will carry on with any other targets that depend on the failed one,
    # generating many more derived failures along the way.
    #
    # We could mask errors in the top-level makefile if any component failed. The trouble is,
    # there could be 2 unrelated errors: a component may fail to build, and the makefile
    # may also fail for another reason (like a bug in the makefile itself).

    $error= "$failedCount component(s) failed to build, see the generated HTML report file:\n" .
            $reportFileMsg .
            "Rerun @{[SCRIPT_NAME]} with the '--resume' switch to try again (but see the documentation for caveats).\n";
  }
  elsif ( $makeExitCode != 0 )
  {
    $error= "The build failed with exit code $makeExitCode, see the generated HTML report file:\n" .
            $reportFileMsg .
            "Rerun @{[SCRIPT_NAME]} with the '--resume' switch to try again (but see the documentation for caveats).\n";
  }
  else
  {
    $finalMsg = "\nThe build was completed successfully. The generated HTML report file is:\n" .
                $reportFileMsg .
                "Suggested environment variable: export OPENRISC_TOOLS=\"$buildDir\"\n";
  }

  write_status( $buildDir, defined( $error ) ? MiscUtils::TRUE : MiscUtils::FALSE );


  my $postBuildAction = $configEntries->{ "PostBuildAction" };

  if ( defined ( $postBuildAction ) and length( $postBuildAction ) > 0 and !defined( $error ) )
  {
    write_stdout( "Running post-build action...\n" );
    my $postBuildActionExitCode = ProcessUtils::run_process( $postBuildAction );

    if ( $postBuildActionExitCode != 0 )
    {
      die "The post-build action failed with exit code $postBuildActionExitCode, the command was:\n" .
          "$postBuildAction\n";
    }
  }

  my $reportFilePath = FileUtils::cat_path( $publicReportsBasedir, $reportFilename );
  run_visual_notification( $reportFilePath, $configEntries, MiscUtils::FALSE );

  if ( defined $error )
  {
    die $error;
  }

  write_stdout( $finalMsg );

  return MiscUtils::EXIT_CODE_SUCCESS;
}


sub run_visual_notification ( $ $ $ )
{
  my $reportFilePath = shift;
  my $configEntries  = shift;
  my $isTestMode     = shift;

  $ENV{ ORBUILD_TOOLS } = TOOLS_DIR;  # In case it's not already set (for example, when running with --test-visual-notification).
  $ENV{ ORBUILD_FINAL_REPORT_FILENAME  } = $reportFilePath;

  my $visualNotification = $configEntries->{ "VisualNotification" };

  if ( defined ( $visualNotification ) and length( $visualNotification ) > 0 )
  {
    write_stdout( "Running the visual notification action...\n" );
    ProcessUtils::run_process_exit_code_0( $visualNotification );
  }
  elsif ( $isTestMode )
  {
      die "The VisualNotification configuration setting is empty.\n";
  }
}


sub create_symbolic_link ( $ $ )
{
  my $linkFilename   = shift;
  my $targetFilename = shift;

  # Do not recreate the link if it already exists and it points to the same target.
  # Otherwise, any Gnome/KDE GUI file manager window open on that link will automatically
  # close or lose its current position, which is annoying.

  my $existingLink = readlink( $linkFilename );

  if ( !defined( $existingLink ) or $existingLink ne $targetFilename )
  {
    my $linkCmd = qq<ln --symbolic --no-dereference --force "$targetFilename" "$linkFilename">;

    my $linkExitCode = ProcessUtils::run_process( $linkCmd );

    if ( $linkExitCode != 0 )
    {
      die qq{Error creating symbolic link "$linkFilename" -> "$targetFilename".\n};
    }
  }
}


sub format_timestamp ( $ $ $ ) 
{
  my $year  = shift;
  my $month = shift;
  my $day   = shift;

  return sprintf( "%04d" . DATE_SEPARATOR . "%02d" . DATE_SEPARATOR . "%02d",
                  $year,
                  $month,
                  $day );
}


sub rotate_build_dir ( $ $ )
{
  my $configEntries = shift;
  my $shouldRotate  = shift;

  my $containingDir = File::Spec->rel2abs( $configEntries->{ "BuildsDir" }, SANDBOX_DIR );

  FileUtils::create_folder_if_does_not_exist( $containingDir );

  my ( @capturedStdout, @capturedStderr );

  my $childProcessName = "RotateDir.pl";

  my $slotPrefix = "build-";

  my $cmd = PERL_INTERPRETER . " " . FileUtils::cat_path( TOOLS_DIR, "RotateDir.pl " );

  if ( $shouldRotate )
  {
    $cmd .= "--slot-count " . $configEntries->{ RotateSlotCount } . " ";
  }
  else
  {
    # The user will be manually managing the build directories.
    $cmd .= "--no-slot-deletion ";
  }

  # WARNING: This is not year 2038 compliant on 32-bit Linux systems and on Cygwin.
  #          See RotateDir.pl for more information about this code.
  my ($sec,$min,$hour,$mday,$mon,$year,$wday,$yday,$isdst) = localtime( time() );
  $mon  += 1;
  $year += 1900;
  my $timestampStr = format_timestamp( $year, $mon, $mday );

  $cmd .= "--dir-name-prefix $slotPrefix " .
          "--dir-naming-scheme date " .
          "--timestamp=$timestampStr " .
          "--output-only-new-dir-name " .
          "$containingDir";

  my $buildDir = ProcessUtils::run_process_capture_single_value_output( $cmd );
  $buildDir = StringUtils::trim_blanks( $buildDir );

  # Note that mkpath() raises an error if it fails.
  File::Path::mkpath( FileUtils::cat_path( $buildDir, BUILD_INTERNAL_REPORTS_SUBDIR   ) );
  File::Path::mkpath( FileUtils::cat_path( $buildDir, BUILD_PUBLIC_REPORTS_BASEDIR, BUILD_PUBLIC_REPORTS_SUBDIR ) );

  File::Path::mkpath( FileUtils::cat_path( $buildDir, BUILD_CHECKOUT_SENTINELS_SUBDIR ) );
  File::Path::mkpath( FileUtils::cat_path( $buildDir, BUILD_UNPACK_SENTINELS_SUBDIR   ) );
  File::Path::mkpath( FileUtils::cat_path( $buildDir, BUILD_AUTOCONF_SENTINELS_SUBDIR ) );
  File::Path::mkpath( FileUtils::cat_path( $buildDir, BUILD_COMMAND_SENTINELS_SUBDIR  ) );

  write_status( $buildDir, MiscUtils::TRUE );
}


#------------------------------------------------------------------------

sub write_status ( $ $ )
{
  my $buildDir = shift;
  my $lastAttemptFailed = shift;

  my $absBuildDir = File::Spec->rel2abs( $buildDir, SANDBOX_DIR );
  my $lastAttemptFailedStr = $lastAttemptFailed ? "failed" : "successful";

  my $contents = "CurrentBuildDir = $absBuildDir\n";
  $contents   .= "LastAttempt = $lastAttemptFailedStr\n";

  FileUtils::write_string_to_new_file( STATUS_FILENAME, $contents );
}


sub read_status ( $ )
{
  my $configEntries = shift;

  my %statusEntries;

  ConfigFile::read_config_file( STATUS_FILENAME, \%statusEntries );

  my @mandatoryEntries = qw( CurrentBuildDir
                             LastAttempt     );

  ConfigFile::check_config_file_contents( \%statusEntries, \@mandatoryEntries, undef, STATUS_FILENAME );

  my $buildDir = $statusEntries{ "CurrentBuildDir" };

  $configEntries->{ "CurrentBuildDir" } = $buildDir;

  my $lastAttempt = lc( $statusEntries{ "LastAttempt" } );

  if ( $lastAttempt ne "successful" and
       $lastAttempt ne "failed" )
  {
    die "The status file contains an invalid value of \"" . $statusEntries{ "LastAttempt" } .
        "\" for setting LastAttempt, valid values are \"successful\" and \"failed\".\n";
  }

  $configEntries->{ "LastAttempt" } = $lastAttempt;
}


sub check_boolean_option ( $ $ )
{
  my $configEntries = shift;
  my $optionName = shift;

  if ( lc( $configEntries->{ $optionName } ) ne "yes" and
       lc( $configEntries->{ $optionName } ) ne "no" )
  {
    die "The configuration contains an invalid value of \"" . $configEntries->{ $optionName } . "\" for setting $optionName, valid values are \"yes\" and \"no\".\n";
  }
}


sub process_config_file_etc ( $ $ )
{
  my $configEntries = shift;
  my $interactive   = shift;

  check_sandbox_layout_version();

  ConfigFile::read_config_file( CONFIG_FILENAME, $configEntries );

  my @mandatoryEntries = qw( RotateSlotCount
                             BuildsDir
                             DownloadCacheDir
                             RepositoriesDir
                             Makefile
                             ParallelBuild
                             InteractiveBuild
                             StopOnFirstError
                           );

  my @optionalEntries = qw( VisualNotification
                            PostBuildAction
                          );

  ConfigFile::check_config_file_contents( $configEntries, \@mandatoryEntries, \@optionalEntries, CONFIG_FILENAME );


  if ( StringUtils::has_non_digits( $configEntries->{ "RotateSlotCount" } ) )
  {
    die "Invalid RotateSlotCount value \"" . $configEntries->{ "RotateSlotCount" } . "\"\n";
  }


  check_boolean_option( $configEntries, "ParallelBuild" );
  check_boolean_option( $configEntries, "InteractiveBuild" );

  if ( lc( $configEntries->{ "ParallelBuild"    } ) eq "yes" and
       lc( $configEntries->{ "InteractiveBuild" } ) eq "yes" )
  {
    die "The configuration enables both settings ParallelBuild and InteractiveBuild at the same time, which is normally an error.\n";
  }

  check_boolean_option( $configEntries, "StopOnFirstError" );

  if ( $interactive )
  {
    $configEntries->{ "ParallelBuild"    } = "no";
    $configEntries->{ "InteractiveBuild" } = "yes";
  }
}


sub check_sandbox_layout_version ()
{
  if ( -e SANDBOX_LAYOUT_VERSION_FILENAME )
  {
    my %layoutVersionEntries;

    ConfigFile::read_config_file( SANDBOX_LAYOUT_VERSION_FILENAME, \%layoutVersionEntries );

    my @mandatoryEntries = qw( SandboxLayoutVersion
                             );

    ConfigFile::check_config_file_contents( \%layoutVersionEntries, \@mandatoryEntries, undef, SANDBOX_LAYOUT_VERSION_FILENAME );

    my $version = $layoutVersionEntries{ "SandboxLayoutVersion" };

    if ( $version != SANDBOX_LAYOUT_VERSION_NUMBER )
    {
      die "This version of @{[APP_NAME]} is not compatible with the current sandbox layout. " .
          "You'll probably need to check out a fresh copy of @{[APP_NAME]} in a new directory.\n";
    }
  }
  else
  {
    my $contents = "SandboxLayoutVersion = @{[SANDBOX_LAYOUT_VERSION_NUMBER]}\n";
    FileUtils::write_string_to_new_file( SANDBOX_LAYOUT_VERSION_FILENAME, $contents );
  }
}


sub is_cygwin ()
{
  return $^O eq 'cygwin';
}


MiscUtils::entry_point( \&main, SCRIPT_NAME );
